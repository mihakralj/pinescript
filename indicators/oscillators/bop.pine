// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Balance of Power (BOP)", "BOP", overlay=false)

//@function Calculates Balance of Power oscillator measuring buyer vs seller strength
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/oscillators/bop.md
//@param length Smoothing period (0 for raw values)
//@returns BOP value measuring relative buying/selling pressure
bop(simple int length) =>
    if length < 0
        runtime.error("Length must be non-negative")

    float raw_bop = high == low ? 0.0 : (close - open) / (high - low)
    if length == 0
        raw_bop
    else
        float alpha = 2.0 / math.max(length, 1)
        var float ema = na
        var float result = na
        var float e = 1.0
        var bool warmup = true
        if not na(raw_bop)
            if na(ema)
                ema := 0
                result := raw_bop
            else
                ema := alpha * (raw_bop - ema) + ema
                if warmup
                    e *= (1 - alpha)
                    float c = 1.0 / (1.0 - e)
                    result := c * ema
                    if e <= 1e-10
                        warmup := false
                else
                    result := ema
        result

// ---------- Main loop ----------

// Inputs
i_smooth = input.int(14, "Smoothing Length", minval=0, tooltip="0 for raw values")

// Calculation
float bop_value = bop(i_smooth)

// Plots
plot(bop_value, "BOP", color.yellow, 2)


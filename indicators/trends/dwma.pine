// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Double Weighted Moving Average", "DWMA", overlay=true)

//@function Calculates DWMA using double weighted smoothing with compensator
//@doc https://github.com/mihakralj/pinescript/blob/main/docs/trends/dwma.md
//@param source Series to calculate DWMA from
//@param period Lookback period for both smoothing passes
//@returns DWMA value, calculates from first bar using available data
dwma(series float src, simple int period) =>
    if period <= 0
        runtime.error("Period must be greater than 0")
    int p = math.max(1, math.min(math.min(bar_index + 1, period), 4000))
    float sum1 = 0.0, float weightSum1 = 0.0
    for i = 0 to p - 1
        if not na(src[i])
            sum1 += src[i] * (p - i)
            weightSum1 += p - i
    float currentWma = weightSum1 > 0 ? sum1 / weightSum1 : na
    var float[] wmaHistory = array.new_float(0)
    array.push(wmaHistory, currentWma)
    if array.size(wmaHistory) > period
        array.shift(wmaHistory)
    float sum2 = 0.0, float weightSum2 = 0.0
    int availableValues = math.min(array.size(wmaHistory), p)
    for i = 0 to availableValues - 1
        float wmaVal = array.get(wmaHistory, availableValues - 1 - i)
        if not na(wmaVal)
            sum2 += wmaVal * (p - i)
            weightSum2 += p - i
    weightSum2 > 0 ? sum2 / weightSum2 : na


// ---------- Main loop ----------

// Inputs
i_period = input.int(10, "Period", minval=1)  // Changed default from 5000 to 20
i_source = input.source(close, "Source")

// Calculation
dwma = dwma(i_source, i_period)

// Plot
plot(dwma, "Custom DWMA", color.new(color.yellow, 0), 2)

// Alert
alertcondition(ta.cross(i_source, dwma), "Price Crosses DWMA", "Price crossed DWMA on {{ticker}}")
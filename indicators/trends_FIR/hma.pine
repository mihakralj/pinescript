// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Hull Moving Average (HMA)", "HMA", overlay=true)

//@function Calculates HMA in a single pass combining all WMA calculations
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_FIR/hma.md
//@param source Series to calculate HMA from
//@param period Lookback period - FIR window size
//@returns HMA value, calculates from first bar using available data
//@optimized Uses three-stage WMA calculation with O(n) complexity per bar
hma(series float source, simple int period) =>
    if period <= 0
        runtime.error("Period must be greater than 0")
    int p = math.min(bar_index + 1, period)
    int half_p = math.max(1, math.round(p / 2))
    int sqrt_p = math.max(1, math.round(math.sqrt(p)))
    float sum_half = 0.0
    float sum_full = 0.0
    float sum_sqrt = 0.0
    float weight_half = 0.0
    float weight_full = 0.0
    float weight_sqrt = 0.0
    for i = 0 to p - 1
        float price = source[i]
        if not na(price)
            float w_half = i < half_p ? half_p - i : 0
            float w_full = p - i
            sum_half += price * w_half
            sum_full += price * w_full
            weight_half += w_half
            weight_full += w_full
    float wma_half = nz(sum_half / weight_half, na)
    float wma_full = nz(sum_full / weight_full, na)
    float diff = na(wma_half) or na(wma_full) ? na : 2.0 * wma_half - wma_full
    for i = 0 to sqrt_p - 1
        float d = diff[i]
        if not na(d)
            float w_sqrt = sqrt_p - i
            sum_sqrt += d * w_sqrt
            weight_sqrt += w_sqrt
    nz(sum_sqrt / weight_sqrt, na)

// ---------- Main loop ----------

// Inputs
i_period = input.int(10, "Period", minval=1)
i_source = input.source(close, "Source")

// Calculation
hma_value = hma(i_source, i_period)

// Plot
plot(hma_value, "HMA", color=color.yellow, linewidth=2)

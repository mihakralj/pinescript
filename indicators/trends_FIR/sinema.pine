// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Sine-weighted Moving Average", "SINEMA", overlay=true)

//@function Calculates SINEMA using sine-wave weighted smoothing with compensator
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_FIR/sinema.md
//@param source Series to calculate SINEMA from
//@param period Lookback period - FIR window size
//@returns SINEMA value, calculates from first bar using available data
sinema(series float source, simple int period) =>
    if period <= 0
        runtime.error("Period must be greater than 0")
    int p = math.max(1, math.min(math.min(bar_index + 1, period), 4000))
    float sum = 0.0
    float weight = 0.0
    for i = 0 to p - 1
        price = source[i]
        if not na(price)
            // Generate sine wave weights across the period
            w = math.sin(math.pi * (i + 1) / p)
            sum += price * w
            weight += w
    weight > 0 ? sum / weight : source

// ---------- Main loop ----------

// Inputs
i_period = input.int(10, "Period", minval=1)
i_source = input.source(close, "Source")

// Calculation
sinema = sinema(i_source, i_period)

// Plot
plot(sinema, "SINEMA", color.new(color.yellow, 0), 2)

// Alert
alertcondition(ta.cross(i_source, sinema), "Price Crosses SINEMA", "Price crossed SINEMA on {{ticker}}")

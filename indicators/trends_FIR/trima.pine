// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Triangular Moving Average (TRIMA)", "TRIMA", overlay=true)

//@function Calculates TRIMA using triangular weighted smoothing with compensator
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_FIR/trima.md
//@param source Series to calculate TRIMA from
//@param period Lookback period - FIR window size
//@returns TRIMA value, calculates from first bar using available data
//@optimized for performance and dirty data
trima(series float source, simple int period) =>
    if period <= 0
        runtime.error("Period must be greater than 0")
    int p = math.min(bar_index + 1, period)
    var array<float> weights = array.new_float(1, 1.0)
    var int last_p = 1
    if last_p != p
        weights := array.new_float(p, 0.0)
        int mid = math.floor(p/2)
        for i = 0 to p - 1
            array.set(weights, i, math.min(i, p - 1 - i) + 1)
        last_p := p
    float sum = 0.0
    float weight_sum = 0.0
    for i = 0 to p - 1
        float price = source[i]
        if not na(price)
            float w = array.get(weights, i)
            sum += price * w
            weight_sum += w
    nz(sum / weight_sum, source)

// ---------- Main loop ----------

// Inputs
i_period = input.int(10, "Period", minval=1)
i_source = input.source(close, "Source")

// Calculation
trima_value = trima(i_source, i_period)

// Plot
plot(trima_value, "TRIMA", color.new(color.yellow, 0), 2)

// Alert
alertcondition(ta.cross(i_source, trima_value), "Price Crosses TRIMA", "Price crossed TRIMA on {{ticker}}")

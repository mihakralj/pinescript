// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Zero-Lag Double EMA (ZLDEMA)", "ZLDEMA", overlay=true)

//@function Calculates ZLDEMA using zero-lag price and double exponential smoothing with compensator
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_IIR/zldema.md
//@param source Series to calculate ZLDEMA from
//@param period Smoothing period
//@param alpha smoothing factor, used to calculate alpha as 2/(period+1)
//@returns ZLDEMA value with zero-lag effect applied
//@optimized for performance and dirty data
zldema(series float source,simple int period=0,simple float alpha=0)=>
    if alpha<=0 and period<=0
        runtime.error("Alpha or period must be provided")
    float a=alpha>0?alpha:2.0/(1+period)
    simple int lag=math.max(1,math.round((period-1)/2))
    var bool warmup=true, var float e=1.0
    var float ema1_raw=0.0, var float ema2_raw=0.0
    var float ema1=source, var float ema2=source
    var priceBuffer=array.new<float>(lag+1,na)
    if not na(source)
        array.shift(priceBuffer)
        array.push(priceBuffer,source)
        float laggedPrice=nz(array.get(priceBuffer,0),source)
        float signal=2*source-laggedPrice
        ema1_raw:=a*(signal-ema1_raw)+ema1_raw
        if warmup
            e*=(1-a)
            float c=1.0/(1.0-e)
            ema1:=c*ema1_raw
            ema2_raw:=a*(ema1-ema2_raw)+ema2_raw
            ema2:=c*ema2_raw
            warmup := e<=1e-10 ? false : true
        else
            ema1:=ema1_raw
            ema2_raw:=a*(ema1-ema2_raw)+ema2_raw
            ema2:=ema2_raw
        2*ema1-ema2
    else
        na


// ---------- Main loop ----------

// Inputs
i_period = input.int(10, "Period", minval=1)
i_source = input.source(close, "Source")

// Calculations
zldema = zldema(i_source, i_period)

// Plots
plot(zldema, "ZLDEMA", color.new(color.yellow, 0), 2)

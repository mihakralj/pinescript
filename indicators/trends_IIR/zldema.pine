// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Zero-Lag Double Exponential Moving Average", "ZLDEMA", overlay=true)

//@function Calculates ZLEMA using zero-lag price and exponential smoothing with compensator
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_IIR/zldema.md
//@param source Series to calculate ZLDEMA from
//@param alpha Smoothing factor (typically 2/(period+1))
//@returns ZLDEMA value with zero-lag effect applied

zldema(series float source, simple float alpha) =>
    if alpha <= 0
        runtime.error("Alpha must be greater than 0")
    var float EPSILON = 1e-10
    var float raw_ema1 = 0.0
    var float raw_ema2 = 0.0
    var float e = 1.0
    int lag = math.round(math.min((1 / alpha) - 0.5, bar_index / 2))
    float zlema_input = 2 * source - source[lag]
    raw_ema1 := alpha * (zlema_input - raw_ema1) + raw_ema1
    e := (1 - alpha) * e
    float compensation = e > EPSILON ? 1.0 / (1.0 - e) : 1.0
    float ema1_compensated = raw_ema1 * compensation
    raw_ema2 := alpha * (ema1_compensated - raw_ema2) + raw_ema2
    compensation * ((1.5 * raw_ema1) - (0.5 * raw_ema2))


// ---------- Main loop ----------

// Inputs
i_period = input.int(2000, "Period", minval=1)
i_source = input.source(close, "Source")

// Calculations
alpha = 2.0/(float(i_period)+1.0)  // Calculate alpha from period
zldema = zldema(i_source, alpha)   // Call function with correct parameters

// Plots
plot(zldema, "ZLDEMA", color.new(color.yellow, 0), 2)

// Alert condition for price crossing ZLDEMA
alertcondition(ta.cross(i_source, zldema), "Price Crosses ZLEMA", "Price crossed ZLEMA on {{ticker}}")

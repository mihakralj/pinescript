// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Triple Exponential Moving Average", "TEMA", overlay=true)

//@function Calculates TEMA using triple exponential smoothing with compensator
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_IIR/tema.md
//@param source Series to calculate TEMA from
//@param alpha smoothing factor
//@returns TEMA value from first bar with proper compensation
tema(series float source, simple float alpha) =>
    if alpha <= 0
        runtime.error("Alpha must be greater than 0")
    var simple float EPSILON = 1e-10        // constant - smallest relevant value
    var float raw_ema1 = 0.0
    var float raw_ema2 = 0.0
    var float raw_ema3 = 0.0
    var float e = 1.0  // compensation factor
    e := (1 - alpha) * e
    float compensation = e > EPSILON ? 1.0 / (1.0 - e) : 1.0
    raw_ema1 := alpha * (source - raw_ema1) + raw_ema1
    raw_ema2 := alpha * (raw_ema1 * compensation - raw_ema2) + raw_ema2
    raw_ema3 := alpha * (raw_ema2 * compensation - raw_ema3) + raw_ema3
    compensation * (3 * raw_ema1 - 3 * raw_ema2 + raw_ema3)


// ---------- Main loop ----------

// Inputs
i_period = input.int(10, "Period", minval=1)
i_source = input.source(close, "Source")

// Calculation
alpha = 2.0/(float(i_period)+1.0)  // calculate alpha from period
tema = tema(i_source, alpha)

// Plot
plot(tema, "Custom TEMA", color.new(color.yellow, 0), 2)

// Alert
alertcondition(ta.cross(i_source, tema), "Price Crosses TEMA", "Price crossed TEMA on {{ticker}}")

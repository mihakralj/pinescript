// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Double Exponential Moving Average", "DEMA", overlay=true)

//@function Calculates DEMA using double exponential smoothing with compensator
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_IIR/dema.md
//@param source Series to calculate DEMA from
//@param alpha smoothing factor
//@returns DEMA value from first bar with proper compensation
//@optimized for performance and dirty data
dema(series float source, simple int period=0, simple float alpha=0)=>
    if alpha<=0 and period<=0
        runtime.error("Alpha or period must be provided")
    float a=alpha>0?alpha:2.0/math.max(period,1)
    var float ema1=na,var float ema2=na,var float e=1.0
    var bool warmup=true
    var float result=na
    if not na(source)
        if na(ema1)
            ema1:=0, ema2:=0
            result:=source
        else
            ema1:=a*(source-ema1)+ema1
            if warmup
                e*=(1-a)
                float c=1.0/(1.0-e)
                ema2:=a*(ema1*c-ema2)+ema2
                result:=c*(2*ema1-ema2)
                if e<=1e-10
                    warmup:=false
            else
                ema2:=a*(ema1-ema2)+ema2
                result:=2*ema1-ema2
    result


// ---------- Main loop ----------

// Inputs
i_period = input.int(10, "Period", minval=1)
i_source = input.source(close, "Source")

// Calculation
dema = dema(i_source, period=i_period)

// Plot
plot(dema, "DEMA", color.new(color.yellow, 0), 2)

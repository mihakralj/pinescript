// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Hull Exponential Moving Average", "HEMA", overlay=true)

//@function Calculates HEMA (Hull Exponential Moving Average) combining multiple EMA calculations
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_IIR/hema.md
//@param source Series to calculate HEMA from
//@param period Lookback period for calculation
//@returns HEMA value that combines fast and slow EMAs with logarithmic weighting
//@optimized for performance and dirty data
hema(series float source, simple int period) =>
    if period <= 0
        runtime.error("Period must be greater than 0")
    float alphaSlow = 2.0 / (float(period) + 1.0)
    float alphaFast = 1.0 - math.pow(1.0 - alphaSlow, 3.0)
    float alphaFinal = 2.0 / (math.sqrt((2.0 / alphaSlow) - 1.0) + 1.0)
    var float emaFast = na
    var float emaSlow = na
    var float hema = na
    if na(source)
        hema
    else
        if na(emaFast)
            emaFast := source
            emaSlow := source
            hema := source
        else
            emaFast := alphaFast * (source - emaFast) + emaFast
            emaSlow := alphaSlow * (source - emaSlow) +  emaSlow
            float diff = 1.693147 * emaFast - 0.693147 * emaSlow
            hema := alphaFinal * (diff - hema) + hema
        hema


// ---------- Main loop ----------

// Inputs
i_period = input.int(10, "Period", minval=1)
i_source = input.source(close, "Source")

// Calculation
hema = hema(i_source, i_period)

// Plot
plot(hema, "Custom HEMA", color.new(color.yellow, 0), 2)


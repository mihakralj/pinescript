// The MIT License (MIT)
// © mihakralj
//@version=6
indicator("Quadruple Exponential Moving Average", "QEMA", overlay=true)

//@function Calculates QEMA using quadruple exponential smoothing with compensator
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_IIR/qema.md
//@param source Series to calculate QEMA from
//@returns QEMA value from first bar with proper compensation
//@optimized for performance and dirty data
qema(series float source, int period = 0, float alpha = 0.0) =>
    // derive α₁ from period if not given
    float a1 = alpha > 0 ? alpha : (period > 0 ? 2.0/(period+1):alpha)
    float r = math.pow(1/a1, 1/3)
    float a2 = a1 * r
    float a3 = a2 * r
    float a4 = a3 * r
    var float ema1 = na, var float ema2 = na, var float ema3 = na, var float ema4 = na
    float result = na
    if not na(source)
        if na(ema1)
            ema1 := source, ema2 := source, ema3 := source, ema4 := source
            result := source
        else
            ema1 := a1*(source - ema1) + ema1
            ema2 := a2*(ema1 - ema2) + ema2
            ema3 := a3*(ema2 - ema3) + ema3
            ema4 := a4*(ema3 - ema4) + ema4
            result := 4*ema1 - 6*ema2 + 4*ema3 - ema4
    result


// ---------- Main loop ----------

// Inputs
i_period = input.int(20, "Period", minval=1)
i_source = input.source(close, "Source")

// Calculation
qema = qema(i_source, i_period) 

// Plot
plot(qema, "Custom QEMA", color.new(color.yellow, 0), 2)

// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Zero-Lag EMA (ZLEMA)", "ZLEMA", overlay=true)

//@function Calculates ZLEMA using zero-lag price and exponential smoothing with compensator
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_IIR/zlema.md
//@param source Series to calculate ZLEMA from
//@param period Smoothing period
//@returns ZLEMA value with zero-lag effect applied
//@optimized for performance and dirty data
zlema(series float source,simple int period=0,simple float alpha=0)=>
    if alpha<=0 and period<=0
        runtime.error("Alpha or period must be provided")
    float a=alpha>0?alpha:2.0/math.max(period,1)
    simple int lag=math.max(1,math.round((period-1)/2))
    var bool warmup=true, var float e=1.0
    var float zlema=0, var float result=source
    var priceBuffer=array.new<float>(lag+1,0.0)
    if not na(source)
        array.shift(priceBuffer)
        array.push(priceBuffer,source)
        float laggedPrice=array.get(priceBuffer,0)
        float signal=2*source-laggedPrice
        zlema:=a*(signal-zlema)+zlema
        if warmup
            e*=(1-a)
            float c=1.0/(1.0-e)
            result:=c*zlema
            warmup := e>1e-10
        else
            result:=zlema
    result

// ---------- Main loop ----------

// Inputs
i_period = input.int(10, "Period", minval=1, tooltip="Default period is 20, adjust as needed")
i_source = input.source(close, "Source")

// Calculation
alpha = 2.0/(float(i_period)+1.0)
zlema = zlema(i_source, i_period)

// Plot
plot(zlema, "ZLEMA", color.new(color.yellow, 0), 2)

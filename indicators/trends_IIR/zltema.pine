// The MIT License (MIT)
// Â© mihakralj
//@version=6
indicator("Zero-Lag Triple Exponential Moving Average (ZLTEMA)", "ZLTEMA", overlay=true)

//@function Calculates ZLTEMA using zero-lag price and triple exponential smoothing with compensator
//@doc https://github.com/mihakralj/pinescript/blob/main/indicators/trends_IIR/zltema.md
//@param source Series to calculate ZLTEMA from
//@param period Smoothing period
//@returns ZLTEMA value with zero-lag effect applied
//@optimized for performance and dirty data
zltema(series float source,simple int period)=>
    if period<=0
        runtime.error("Period must be greater than 0")
    simple float alpha=2.0/(period+1.0)
    simple int lag=math.max(1,math.round((period-1)/2))
    var priceBuffer=array.new<float>(lag+1,0.0)
    var float raw_ema1=na
    var float raw_ema2=na
    var float raw_ema3=na
    var float zltemaValue=na
    if not na(source)
        if na(zltemaValue)
            zltemaValue:=source
            raw_ema1:=source
            raw_ema2:=source
            raw_ema3:=source
            array.fill(priceBuffer,source)
        else
            array.shift(priceBuffer)
            array.push(priceBuffer,source)
            float laggedPrice=array.get(priceBuffer,0)
            float zlema_input=2*source-laggedPrice
            raw_ema1:=alpha*(zlema_input-raw_ema1)+raw_ema1
            raw_ema2:=alpha*(raw_ema1-raw_ema2)+raw_ema2
            raw_ema3:=alpha*(raw_ema2-raw_ema3)+raw_ema3
            zltemaValue:=3*raw_ema1-3*raw_ema2+raw_ema3
    zltemaValue


// ---------- Main loop ----------

// Inputs
i_period = input.int(10, "Period", minval=1)
i_source = input.source(close, "Source")

// Calculations
zltema = zltema(i_source, i_period) 

// Plots
plot(zltema, "ZLTEMA", color.new(color.yellow, 0), 2)
